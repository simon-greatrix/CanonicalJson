plugins {
  id 'java'
  id 'java-library'
  id 'maven-publish'
  id 'checkstyle'
  id "com.github.spotbugs"
  id 'com.palantir.git-version'
  id 'signing'
  id 'org.owasp.dependencycheck'
}

group = 'com.pippsford'
if ((versionDetails().branchName ==~ /^(master)|(release.*)|(patch.*)$/) && versionDetails().isCleanTag) {
  project.version = gitVersion()
} else {
  project.version = '100-SNAPSHOT'
}

tasks.withType(JavaCompile).configureEach {
  options.encoding = 'UTF-8'
}

checkstyle {
  toolVersion = "10.13.0"
  configFile = rootProject.file('config/checkstyle/checkstyle.xml')
}

checkstyleTest {
  enabled = false
}

// Guava conflict with checkstyle plugin.
configurations.checkstyle {
  resolutionStrategy.capabilitiesResolution.withCapability("com.google.collections:google-collections") {
    select("com.google.guava:guava:0")
  }
}

spotbugs {
  excludeFilter = rootProject.file('config/spotbugs/spotbugs-exclude.xml')
}

spotbugsMain {
  reports {
    html {
      required = true
    }
    xml {
      required = false
    }
  }
}

// Do not perform spotbugs analysis on test code
spotbugsTest {
  enabled = false
}

repositories {
  mavenCentral()
}

dependencies {
  api group: 'jakarta.json', name: 'jakarta.json-api', version: '2.1.3'

  // We need this library for the Longest Common Subsequence solver which is needed for creating patches
  implementation group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
  compileOnly group: 'jakarta.annotation', name: 'jakarta.annotation-api', version: '3.0.0'
  compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.20.1'
  compileOnly group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.20.1'
  compileOnly group: 'tools.jackson.core', name: 'jackson-core', version: '3.0.3'
  compileOnly group: 'tools.jackson.core', name: 'jackson-databind', version: '3.0.3'
  compileOnly group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '4.0.1'
  compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.2.0'

  testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.2.4'
  testImplementation group: 'tools.jackson.dataformat', name: 'jackson-dataformat-smile', version: '3.0.3'
  testImplementation group: 'com.fasterxml.jackson.dataformat', name: 'jackson-dataformat-smile', version: '2.20.1'
  testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.20.1'
  testImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.20.1'

  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.9.2'

  spotbugsPlugins 'com.h3xstream.findsecbugs:findsecbugs-plugin:1.13.0'
}

java {
  withSourcesJar()
  withJavadocJar()
  toolchain {
    languageVersion = JavaLanguageVersion.of(17)
  }
}

compileJava {
  options.compilerArgs << '-parameters'
  options.compilerArgs << '-Xlint:deprecation'
}

compileTestJava {
  options.compilerArgs << '-parameters'
  options.compilerArgs << '-Xlint:deprecation'
}

javadoc {
  options.addStringOption('Xmaxwarns', '65536')
}

test {
  useJUnitPlatform()
}

artifacts {
  archives javadocJar, sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      pom {
        name = 'Canonical JSON'
        packaging = 'jar'
        // optionally artifactId can be defined here
        description = 'An implementation of the Canonical JSON format with support for javax.json and Jackson'
        url = 'https://github.com/simon-greatrix/CanonicalJson.git'

        scm {
          connection = 'scm:git:https://github.com/simon-greatrix/CanonicalJson.git'
          developerConnection = 'scm:git:https://github.com/simon-greatrix/CanonicalJson.git'
          url = 'https://github.com/simon-greatrix/CanonicalJson.git'
        }

        licenses {
          license {
            name = 'The Apache License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id = 'simon-greatrix'
            name = 'Simon Greatrix'
            email = 'simon@pippsford.com'
            organization = ''
            organizationUrl = ''
          }
        }
      }
    }
  }

  repositories {
    maven {
      name = 'file'
      url = layout.buildDirectory.dir('repository')
    }
  }
}

signing {
  useGpgCmd()
  sign publishing.publications.mavenJava
}

tasks.register("createCentralZip", Zip) {
  dependsOn tasks.named("publishMavenJavaPublicationToFileRepository")
  from layout.buildDirectory.dir("repository")
  destinationDirectory = layout.buildDirectory.dir("publications")
  archiveFileName = "central.zip"
}

